# Luke's config for the Zoomer Shell

# Load version control information:
autoload -Uz vcs_info
precmd() { vcs_info }

# Format the vcs_info_msg_0_ variable:
zstyle ':vcs_info:git:*' formats '(%b)'
 
# Enable colors:
autoload -U colors && colors

# Get rid off strange percent sign if you start typing fast after zsh start:
unsetopt PROMPT_SP

# Set prompt (with git branch name):
setopt PROMPT_SUBST
PS1='%B%{$fg[red]%}[%{$fg[yellow]%}%n%{$fg[green]%}@%{$fg[blue]%}%M%{$fg[green]%}${vcs_info_msg_0_} %{$fg[magenta]%}%c%{$fg[red]%}]%{$reset_color%}$%b '

# Automatically cd into typed directory.
setopt autocd
# Disable ctrl-s to freeze terminal.
stty stop undef
setopt interactive_comments

# History in cache directory:
HISTSIZE=10000000
SAVEHIST=10000000
HISTFILE="${XDG_DATA_HOME:-$HOME/.local/share}/zsh/history"

# Load aliases if existent:
[ -f "${XDG_CONFIG_HOME:-$HOME/.config}/shell/aliasrc" ] &&
	source "${XDG_CONFIG_HOME:-$HOME/.config}/shell/aliasrc"

# Basic auto/tab complete:
autoload -U compinit
zstyle ':completion:*' menu select
zmodload zsh/complist
compinit
# include hidden files
_comp_options+=(globdots)

# Vim mode:
bindkey -v
export KEYTIMEOUT=1

# Use vim keys in tab complete menu:
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# Use reverse search when searching in vim mode:
bindkey -M vicmd '/' history-incremental-search-backward

# Use lf to switch directories and bind it to Ctrl-O:
lfcd () {
    tmp="$(mktemp)"
    lf -last-dir-path="$tmp" "$@"
    if [ -f "$tmp" ]; then
        dir="$(cat "$tmp")"
        rm -f "$tmp" >/dev/null
        [ -d "$dir" ] && [ "$dir" != "$(pwd)" ] && cd "$dir"
    fi
}

bindkey -s '^o' 'lfcd\n'
bindkey -s '^b' 'bc -lq\n'
bindkey -s '^f' 'cd "$(dirname "$(fzf)")"\n'
bindkey '^[[P' delete-char

# Edit line in vim with Ctrl-I:
autoload edit-command-line; zle -N edit-command-line
bindkey '^e' edit-command-line

# Load fast goind back to updir:
source /usr/share/zsh/plugins/zsh-bd/bd.zsh 2>/dev/null
autoload -Uz bd

# Load autosuggestions:
source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.plugin.zsh 2>/dev/null

# Load substring search insted of only prefix search:
source /usr/share/zsh/plugins/zsh-history-substring-search/zsh-history-substring-search.zsh

# Load syntax highlighting; should be last:
source /usr/share/zsh/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh 2>/dev/null
